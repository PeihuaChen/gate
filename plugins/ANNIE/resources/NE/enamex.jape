// enamex.jape
// diana, 13 Sep 00
// last modified diana, 17 Apr 01

Phase:	Enamex
//Input: Token SpaceToken Lookup
Options: control = appelt

///////////////////////////////////////////////////////////////

// Person Rules

/////////////////////////////////////////////////////////////////
Macro: TITLE
(
 {Lookup.majorType == title}
 (SPACE)?
)

Macro: INITIALS
(
  ({Token.orth == upperInitial, Token.length =="1"}
  ({Token.string == "."})?
  (SPACE)?
  )+
)

Macro: INITIALS2

(
 {Token.orth == allCaps, Token.length == "2"} |
 {Token.orth == allCaps, Token.length == "3"}
)


Macro: FIRSTNAME
(
 {Lookup.majorType == person_first, Lookup.minorType == male}|
 {Lookup.majorType == person_first, Lookup.minorType == female}|
 (INITIALS)
)

Macro: FIRSTNAMEAMBIG
(
 {Lookup.majorType == person_first, Lookup.minorType == ambig}
)

Macro: LASTNAME
(
 {Token.orth == upperInitial} |
  {Token.orth == allCaps} |
 {Token.orth == mixedCaps}
)


Macro: PERSONENDING
(
 ({Token.string == "Jr"} ({Token.string == "."})? ) |
 ({Token.string == "jr"} ({Token.string == "."})? ) |
 ({Token.string == "Sr"} ({Token.string == "."})? ) |
 ({Token.string == "sr"} ({Token.string == "."})? ) |
 {Token.string == "II"} |
 {Token.string == "III"}
)

Macro: DET
(
 {POS.category == DT}
)


///////////////////////////////////////////////////////////
// Person Rules

Rule:	GazPerson
Priority: 50
(
 {Lookup.majorType == person_full, Lookup.minorType == normal}
)
:person -->
  :person.TempPerson = {kind = "personName", rule = GazPerson}


Rule:	GazPersonFirst
Priority: 20
(
 {Lookup.majorType == person_first, Lookup.minorType == male}|
 {Lookup.majorType == person_first, Lookup.minorType == female}
)
:person 
( 
 (SPACE)
 {Token.orth == upperInitial, Token.length == "1"}
)?
-->
  :person.TempPerson = {kind = "personName", rule = GazPersonFirst}


Rule: PersonFirstContext
Priority: 30
// Anne and Kenton

(
 {Lookup.majorType == person_first}
):personName1
(
 SPACE
 {Token.string == "and"}
 SPACE
)
({Token.orth == upperInitial})
:personName2
 -->
  :personName1.TempPerson = {kind = "personName", rule = PersonFirstContext},
:personName2.TempPerson = {kind = "personName", rule = PersonFirstContext}


Rule: PersonFirstContext2
Priority: 40
// Anne and I

(
 {Lookup.majorType == person_first}
):personName1
(
 SPACE
 {Token.string == "and"}
 SPACE
)
({Token.length == "1"})
:personName2
 -->
  :personName1.TempPerson = {kind = "personName", rule = PersonFirstContext2}


Rule:	PersonTitle
Priority: 15
// Mr. Jones
// Mr Fred Jones

(
 (TITLE)
 ((FIRSTNAME | FIRSTNAMEAMBIG | INITIALS2)
  (SPACE)? 
 )*
 (LASTNAME)
 (SPACE PERSONENDING)?
)
:person -->
  :person.TempPerson = {kind = "personName", rule = "PersonTitle"}

Rule: 	FirstPersonStop
Priority: 20
// surname contains stop words
// e.g. Mary And

(
 (FIRSTNAME (SPACE)?)+
):person
(
 (SPACE {Lookup.majorType == stop}
  (SPACE)?
 )
)
-->
  :person.TempPerson = {kind = "personName", rule = "FirstPersonStop"}

Rule: NotFirstPersonStop
Priority: 50
// ambig first name and surname is stop word
// e.g. Will And

(
 ((FIRSTNAMEAMBIG (SPACE)?)+ | 
  {POS.category == PRP}|
  {POS.category == DT}
 )
 (SPACE {Lookup.majorType == stop}
  (SPACE)?
 )
)
:person -->
  :person.TempPerson = {kind = "negative", rule = "NotFirstPersonStop"}


Rule: NotPersonFull
Priority: 50
// A British

(
 {POS.category == DT}
 (SPACE)
)
(
 (LASTNAME)
):unknown
(
 (SPACE PERSONENDING)?
)
-->
{}

Rule: 	PersonFull
Priority: 10
// F.W. Jones
// Fred Jones

(
 ((FIRSTNAME | FIRSTNAMEAMBIG) (SPACE)? )+
 (LASTNAME)
 (SPACE PERSONENDING)?
)
:person -->
  :person.TempPerson = {kind = "personName", rule = "PersonFull"}

Rule: NotPersonFullReverse
Priority: 20
// XYZ, I
(
 (LASTNAME)
 {Token.string == ","}
 (SPACE)?
 {POS.category == PRP}
 (SPACE PERSONENDING)?
)
:unknown 
-->
{}

Rule: 	PersonFullReverse
Priority: 5
// Jones, F.W.
// don't allow Jones, Fred because too ambiguous
// Smith, TF

(
 (LASTNAME)
 {Token.string == ","}
 (SPACE)?
 (INITIALS (SPACE)? )+ 
 (SPACE PERSONENDING)?
)
:person -->
  :person.TempPerson = {kind = "personName", rule = "PersonFullReverse"}


Rule:  PersonSaint
Priority: 50
// Note: ensure that it's not a Saints Day first
(
 ({Token.string == "St"} ({Token.string == "."})? |
 {Token.string == "Saint"})
 SPACE
 (FIRSTNAME)
 )
:person -->
  :person.TempPerson = {kind = "personName", rule = "PersonSaint"}


Rule: PersonLocAmbig
Priority: 40
// Ken London
// Susan Hampshire

// Christian name + Location --> Person's Name
(
 ((FIRSTNAME| INITIALS2) SPACE 
  {Lookup.majorType == location})
)
:person -->
  :person.TempPerson = {kind = "personName", rule = "PersonLocAmbig"}


///////////////////////////////////////////////////////////////////
// Organisation Rules

Macro:  CDG
// cdg is something like "Ltd."
 (
  ({Lookup.majorType == cdg})|
  ({Token.string == ","} 
  (SPACE)
  {Lookup.majorType == cdg})
 )


Macro: SAINT
(
 ({Token.string == "St"} ({Token.string == "."})? |
 {Token.string == "Saint"})
)

Macro: CHURCH
(
{Token.string == "Church"}|{Token.string == "church"}|
{Token.string == "Cathedral"}|{Token.string == "cathedral"}|
{Token.string == "Chapel"}|{Token.string == "chapel"}
)

/////////////////////////////////////////////////////////////
Rule:	GazOrganization
Priority: 15

(
{Lookup.majorType == organization}
(SPACE {Lookup.majorType == organization})?
)
:orgName -->  
  :orgName.TempOrganization = {kind = "orgName", rule=GazOrganization}

Rule:	LocOrganization
Priority: 20
// Ealing Police
(
 ({Lookup.majorType == location} |
  {Lookup.majorType == country_adj})
 SPACE
{Lookup.majorType == organization}
(SPACE {Lookup.majorType == organization})?
)
:orgName -->  
  :orgName.TempOrganization = {kind = "orgName", rule=LocOrganization}


Rule:	OrgXandY
Priority: 20

// Bradford & Bingley
// Bradford & Bingley Ltd
(
 {POS.category == IN}
 (SPACE)
)?

(
 ({POS.category == NNP}
  (SPACE)? )+

 {Token.string == "&"}

 ((SPACE)?
  {Token.orth == upperInitial}
 )+

 (SPACE CDG)?

)
:orgName -->
  :orgName.TempOrganization = {kind = "orgName", rule = "OrgXandY"}


Rule:OrgUni
Priority: 25
// University of Sheffield
// Sheffield University
// A Sheffield University
(
 {Token.string == "University"}
 SPACE
 {Token.string == "of"}
 (SPACE 
 {Token.orth == upperInitial})+
)
:orgName -->
  :orgName.TempOrganization = {kind = "orgName", rule = "OrgDept"}



Rule: OrgDept
Priority: 25
// Department of Pure Mathematics and Physics

(
 {Token.string == "Department"}
 SPACE
 {Token.string == "of"}
 (SPACE 
 {Token.orth == upperInitial})+
 (SPACE
  {Token.string == "and"}
  (SPACE 
   {Token.orth == upperInitial})+
 )?
)
:orgName -->
  :orgName.TempOrganization = {kind = "orgName", rule = "OrgDept"}



Rule:	OrgXKey
Priority: 15

// Aaaa Ltd.
// Xxx Services Ltd. 
// but NOT A XXX Services Ltd.
(
 {POS.category == DT}
 (SPACE)
)?
(
  ({Token.orth == upperInitial} | 
   {Token.orth == allCaps}|
   {Token.orth == mixedCaps}
  )
  SPACE
  (({Token.orth == upperInitial} | {Token.orth == allCaps})
   SPACE)*
 {Lookup.majorType == org_key}
 (SPACE {Lookup.majorType == org_ending})?
)
:orgName -->
  :orgName.TempOrganization = {kind = "orgName", rule = "OrgXKey"}


Rule:	OrgXEnding
Priority: 20

// Coca Cola Co.

(
 {POS.category == DT}
 (SPACE)
)?
(
  ({Token.orth == upperInitial} | 
   {Token.orth == allCaps}|
   {Token.orth == mixedCaps})
  SPACE
  (({Token.orth == upperInitial} |
   {Token.orth == allCaps})
   SPACE)?
 {Lookup.majorType == cdg}
)
:orgName -->
  :orgName.TempOrganization = {kind = "orgName", rule = "OrgXEnding"}



Rule:	OrgXandYKey
Priority: 20

// Aaaa Ltd.
// Xxx Services Ltd. 
// AA and BB Services Ltd.
// but NOT A XXX Services Ltd.
(
 {POS.category == DT}
 (SPACE)
)?
(
  ({Token.orth == upperInitial} | 
   {Token.orth == allCaps})
  SPACE
  (({Token.string == "and"} | 
    {Token.string == "&"})
   SPACE
   ({Token.orth == upperInitial} | 
   {Token.orth == allCaps})
   SPACE
  )
  (({Token.orth == upperInitial}| 
    {Token.orth == allCaps})
   SPACE)*
 {Lookup.majorType == org_key}
 (SPACE {Lookup.majorType == org_ending})?
)
:orgName -->
  :orgName.TempOrganization = {kind = "orgName", rule = "OrgXandYKey"}


Rule:	OrgXsKeyBase
Priority: 20
 
// Gandy's Circus
// Queen's Ware

(
  (({Token.orth == upperInitial} | 
    {Token.orth == allCaps})
   SPACE)?
  (({Token.orth == upperInitial} | 
    {Token.orth == allCaps})
   SPACE)?
  ({Token.orth == upperInitial}
   {Token.string == "'"}
   ({Token.string == "s"})?
   SPACE)
 ({Lookup.majorType == org_key}|
  {Lookup.majorType == org_base})
)
:orgName -->
  :orgName.TempOrganization = {kind = "orgName", rule = "OrgXsKeybase"}



Rule: NotOrgXBase
Priority: 100
// not things like British National
// or The University
// NOTE: not sure if we want this for emails or not

(
 (DET SPACE)?
)
(
 ({Lookup.majorType == country_adj}|
  {Token.orth == lowercase})
 SPACE
 ({Lookup.majorType == org_base}|
  {Lookup.majorType == govern_key})
)
:orgName -->
  :orgName.Temp = {kind = "notorgName", rule = "NotOrgXBase"}


Rule:	OrgXBase
Priority: 30

// same as OrgXKey but uses base instead of key
// includes govern_key e.g. academy
// Barclays Bank
// Royal Academy of Art
(
 (
  {POS.category == DT}
  SPACE
 )?
)
(
 ({Token.orth == upperInitial} | 
  {Token.orth == allCaps} |
  {Token.orth == mixedCaps}
 )
  SPACE
  ({Token.orth == upperInitial}
   SPACE)?
 ({Token.orth == upperInitial}
   SPACE)?
 ({Lookup.majorType == org_base}|
  {Lookup.majorType == govern_key})
 (SPACE
  {Token.string == "of"}
  SPACE
  {Token.orth == upperInitial}
  (SPACE
  {Token.orth == upperInitial}
  )?
 )?
)
:orgName -->
  :orgName.TempOrganization = {kind = "orgName", rule = "OrgXBase"}


Rule:	OrgPreX
Priority: 30

// Royal Tuscan

(
 {Lookup.majorType == org_pre}
 (SPACE
  {Token.orth == upperInitial})+
 (SPACE {Lookup.majorType == org_ending})?
)
:orgName -->
  :orgName.TempOrganization = {kind = "orgName", rule = "OrgPreX"}




Rule: OrgChurch
Priority: 50
// St. Andrew's Church

(
  (SAINT)
  SPACE
  {Token.orth == upperInitial}
  {Token.string == "'"}({Token.string == "s"})?
  SPACE
  (CHURCH)
)
:orgName -->
  :orgName.TempOrganization = {kind = "orgName", rule = "OrgChurch"}


Rule:OrgPersonAmbig
Priority: 30
// Alexandra Pottery should be org not person
// overrides PersonFull

(
 (FIRSTNAME (SPACE)? )
 ({Lookup.majorType == org_key}|
  {Lookup.majorType == org_base})
 (SPACE {Lookup.majorType == org_ending})?
)
:org 
-->
 :org.TempOrganization= {kind = "orgName", rule = "OrgPersonAmbig"}

 

/////////////////////////////////////////////////////////////////
// Location rules


Rule: 	Location1
Priority: 25
// Lookup = city, country, province, region, water

// Western Europe
// South China sea
(
 (
  {POS.category == DT}
  SPACE
 )?
)
(
 ({Lookup.majorType == loc_key, Lookup.minorType == pre}
  SPACE)?
 {Lookup.majorType == location}
 (SPACE
  {Lookup.majorType == loc_key, Lookup.minorType == post})?
)
:locName -->
  :locName.TempLocation = {kind = "locName", rule = "Location1"}


Rule:	GazLocation
Priority: 50
(
 (
  {POS.category == DT}
  SPACE
 )?
)
  (
	({Lookup.majorType == location})
  )
:locName
  --> 	
 :locName.TempLocation = {kind = "locName", rule=GazLocation}


/////////////////////////////////////////////////////////////////


// Context-based Rules


Rule: FirstNameContext
Priority: 40
// Dear hiranya

(
 {Token.string == "Dear"}
 (SPACE)
)
(
 ({Token.orth == upperInitial} |
  {Token.orth == lowercase})
)
:person -->
  :person.TempPerson = {kind = "personName", rule = "FirstNameContext"}


Rule:InLoc1

(
 {Token.string == "in"}
 (SPACE)
)
(
 {Lookup.majorType == location, Token.orth == allCaps}
)
:locName
-->
 :locName.TempLocation = {kind = "locName", rule = InLoc1}












