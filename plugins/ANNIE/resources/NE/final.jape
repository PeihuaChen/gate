// final.jape
// diana, 05 Jan 01
// last modified diana, 17 Apr 01

//note: organization should be included as part of the address

Phase:	Final
Options: control = appelt

///////////////////////////////////////////////////////////////


Rule: PersonFinal
Priority: 10
(
 {TempPerson.kind == personName}
)
:person
--> 
:person.Person = {rule = "PersonFinal"}



Rule:	OrgCountryFinal
Priority: 50
// G M B Scotland
// Scottish Electricity Board

(
 ({TempOrganization} SPACE)?

 (({Lookup.majorType == country_adj}|
   {Lookup.majorType == location})
  (SPACE) 
  {TempOrganization}
 )|
 ({TempOrganization}
  (SPACE)
  ({Token.position == startpunct})?
  ({Lookup.majorType == country_adj}|
   {Lookup.majorType == location})
  ({Token.position == endpunct})?
 )
 (SPACE {TempOrganization})?
 (SPACE {Lookup.majorType == org_ending})?
)
:org
-->
  :org.Organization = {rule = "OrgCountryFinal"}


Rule: OrgFinal
Priority: 10
(
 {TempOrganization}
)
:org
--> 
:org.Organization = {rule = "OrgFinal"}


Rule: LocFinal
Priority: 10
(
 {TempLocation}
)
:loc
--> 
:loc.Location = {rule = "LocFinal"}

//////////////////////////////////////////////////////////////
// Rules from Timex

//Date Only Rules

Rule: DateOnlyFinal
Priority: 10
(
 {TempDate}
)
:date 
-->
 :date.Date = {kind = "date", rule = "DateOnlyFinal"}

Rule: DateYearFinal
Priority: 10
(
 {TempDate}
 (
 ({Token.string == ","})?
 SPACE {TempDate})?
 SPACE
 {TempYear}
 (SPACE {TempDate})?
)
:date
-->
 :date.Date = {kind = "date", rule = "DateYearFinal"}

// Date + Time Rules

Rule: DateTimeFinal
Priority: 20
// Friday 10 January 2000 2pm

(
 {TempDate}
 (
  ({Token.string == ","})?
  SPACE {TempDate})?
 (SPACE | {Token.string == ":"})?
 {TempTime}
 (SPACE {TempYear})?
 (SPACE {TempZone})?
)
:date
-->
 :date.Date = {kind = "dateTime", rule = "DateTimeFinal"}

Rule: TimeDateFinal
Priority: 10
// 2pm 10 January 2000
// 2pm 10 January 2000 +0400
(
 {TempTime}
 (SPACE | {Token.string == ":"})?
 {TempDate} 
 (SPACE {TempYear})?
 (SPACE {TempZone})?
)
:date
-->
 :date.Date = {kind = "dateTime", rule = "TimeDateFinal"}


Rule: TimeYearFinal
Priority: 10
// 21:00:00 2000 +0400

(
 {TempTime}
 (SPACE | {Token.string == ":"})? 
 (SPACE {TempYear})
 (SPACE {TempZone})?
)
:date
-->
 :date.Date = {kind = "dateTime", rule = "TimeYearFinal"}


Rule: SeasonYearFinal
Priority: 15
(
 ({Token.string == "spring"} |
  {Token.string == "Spring"})
 (SPACE)
 {TempYear.kind == positive}
)
:date
-->
 :date.Date = {kind = "date", rule = "SeasonYearFinal"}

Rule: YearOnlyFinal
Priority: 10
(
 {TempYear.kind == positive}
)
:date
-->
 :date.Date = {kind = "date", rule = "YearOnlyFinal"}


Rule: TimeOnlyFinal
Priority: 10
(
 {TempTime.kind == positive}
)
:date
-->
 :date.Date = {kind = "time", rule = "TimeOnlyFinal"}


Rule: TimeContextFinal
Priority: 10
// Wednesday [mdash ] 8-15

(
 ({TempTime}|{TempDate}):date
 (SPACE)?
 {Token.string == "["}
 {Token.string == "mdash"}
 (SPACE)?
 {Token.string == "]"}
 (SPACE)?
)
( {TempTime.kind == temp}
):time
-->
 :date.Date = {kind = "date", rule = "TimeContextFinal"},
 :time.Date = {kind = "time", rule = "TimeContextFinal"}

Rule: TimeWordsContextFinal
Priority: 50

//seven to nine o'clock
(
 {TempTime.kind == timeWords}
 (SPACE)
 {Token.string == "to"}
 (SPACE)
 {TempTime.kind == positive}
)
:time
-->
  :time.Date = {kind = "time", rule = TimeWordsContextFinal}

Rule: TimeOnlyFinal
Priority: 10
(
 {TempTime.kind == positive}
)
:date
-->
 :date.Date = {kind = "time", rule = "TimeOnlyFinal"}


////////////////////////////////////////////////////////////
Rule: AddressFull
Priority: 100
(
 ({Street}
 {Token.string == ","})? (SPACE)*
 ({TempLocation} 
  ({Token.string == ","})? (SPACE)*
 )+
 ({Postcode})
 ({Token.string == ","})? (SPACE)*
 ({TempLocation})?
)
:address
-->
 :address.Address = {kind = "complete", rule = "AddressFull"}


Rule: EmailFinal
Priority: 50
(
{Email}
)
:address
-->
 :address.Address = {kind = "email", rule = "EmailFinal"}


Rule: PhoneFinal
Priority: 50
(
{Phone}
)
:address
-->
 :address.Address = {kind = "phone", rule = "PhoneFinal"}


Rule: PostcodeFinal
Priority: 50
(
{Postcode}
)
:address
-->
 :address.Address = {kind = "postcode", rule = "PostcodeFinal"}


Rule: IpFinal
Priority: 50
(
{Ip}
)
:address
-->
 :address.Address = {kind = "ip", rule = "IpFinal"}


Rule: UrlFinal
Priority: 50
(
{Url}
)
:address
-->
 :address.Address = {kind = "url", rule = "UrlFinal"}


Rule: StreetFinal
Priority: 50
(
{Street}
)
:address
-->
 :address.Address = {kind = "street", rule = "StreetFinal"}

////////////////////////////////////////////////////////////


Rule: IdentifierFinal
Priority: 10

(
 {TempIdentifier}
)
:ident
-->
 :ident.Identifier = {rule = "IdentifierFinal"}

//////////////////////////////////////////////////////
Rule: UnknownTempPerson
Priority: 5
( 
 {POS.category == NNP}
 ((SPACE|{Token.string == "-"})
  {POS.category == NNP})*
):unknown
 (SPACE)
(
 {TempPerson}
):person
-->
 :unknown.Unknown = {kind = "PN", rule = UnknownTempPerson},
 :person.Person = {rule = UnknownTempPerson}

Rule:Unknown
( 
 {POS.category == NNP}
// ((SPACE|{Token.string == "-"})
// {POS.category == NNP})*
) 
:unknown
-->
 :unknown.Unknown = {kind = "PN", rule = Unknown}
