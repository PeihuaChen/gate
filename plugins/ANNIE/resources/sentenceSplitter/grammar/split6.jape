// Valentin Tablan, 17/05/2001


Phase:split6
Input: Token Split
Options: control = first

//generates the sentence annotations

Rule: r1
(
 ({Token})+
 ({Split.type=="."}|{Split.type=="punctuation"}|{Split.type=="multi"})
 ) :left
-->
:left.Sentence = {}


Rule: r2
(
 ({Token})+
 ) :left
({Split.type=="CR"}|{Split.type=="quote"})
-->
:left.Sentence = {}

// blah blah ."
//the sentence will include the full stop but not the quote

Rule: r3
(
 ({Token})+
 ) :left
({Split.type=="splitAndQuote"}):split
-->
{
	gate.AnnotationSet sentence = (gate.AnnotationSet)bindings.get("left");
	gate.AnnotationSet splitSet = (gate.AnnotationSet)bindings.get("split");
	gate.Annotation firstSplit = (gate.Annotation)
	                              ((gate.Annotation)splitSet.iterator().next()).
	                              getFeatures().get("firstSplit");
	sentence.add(firstSplit);
	gate.FeatureMap features = Factory.newFeatureMap();
	annotations.add(sentence.firstNode(), sentence.lastNode(), "Sentence", features);	
  //add back the quote split
	splitSet.remove(firstSplit);
	features = Factory.newFeatureMap();
	features.put("type", "quote");
	annotations.add(firstSplit.getEndNode(), splitSet.lastNode(), "Split", features);	
}

